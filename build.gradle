apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply from: file("etc/emma.gradle")

group = 'io.qdb'
version = '0.4.1'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.10'
}

project.sourceCompatibility = "1.6"
project.targetCompatibility = "1.6"

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    required = false
    sign configurations.archives
}

def sonatypeUsername = properties.sonatypeUsername
def sonatypePassword = properties.sonatypePassword

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }
            if (version.endsWith("SNAPSHOT")) {
                repository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            } else {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }
            pom.project {
                name 'qdb-buffer'
                packaging 'jar'
                description 'Disk based message queue supporting sequential retrieval of old messages by id and ' +
                        'timestamp for JVM applications.'
                url 'http://qdb.io/qdb-buffer'
                scm {
                    url 'scm:git@github.com:qdb-io/qdb-buffer.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'davidtinker'
                        name 'David Tinker'
                    }
                }
            }
        }
    }
}
